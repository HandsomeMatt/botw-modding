// BoTW TSCB
// Used to index heightmap files in the Terrain folder
struct TSCB_HEADER {
  char magic[4];
  uint32 unk0;
  uint32 unk1;
  uint32 stringTableOffset; // Offset to TSCB_NAMETABLE
  
  float unk2;
  float unk3;
  uint32 lookupStructCount; // Number of UNK_STR0 entries
  uint32 fileCollectionCount; // Number of entries in TSCB_NAMETABLE, maybe something else also (recent discovery: also the number of UNK_STR1 entries)
} header;

// Header describing some unknown structure
local int lookupheader0Start;
struct LOOKUP_HEADER0 {
  uint32 unk0;
  uint32 unk1;
  float unk2;
  uint32 unk3;

  lookupheader0Start = FTell();

  uint32 size;
} lookup_header0;

// Now, a list of offsets to entries in the unknown structure
local int skipPos;
local int lastPos;
struct {
  struct UNK_STR0 {
    lastPos = FTell();
    skipPos = ReadUInt();

    FSkip(skipPos);

    uint32 index;
    float r;
    float g;
    float b;
    float a;

    FSeek(lastPos);
    FSkip(4);
  } unk_str0[header.lookupStructCount] <optimize=false>;
} lookupCollection;

// Same thing, list of offsets to some unknown struct
FSeek(lookupheader0Start + lookup_header0.size);

string UNKSTR1READ(struct UNK_STR1& s)
{
  return s.fileName;
}

local int strReadRestorePos;
struct {
  struct FILE_STR {
    
    lastPos = FTell();
    skipPos = ReadUInt();

    FSkip(skipPos);

    float positionX;
    float positionY;
    float scale;
    float unk3;
    float unk4;
    float unk5;
    float unk6;
    uint32 unk7; // row number

    uint32 strOffset;

    strReadRestorePos = FTell();
    FSkip(strOffset - 4); // subtract the size of stroffset

    string fileName;
    FSeek(strReadRestorePos);

    uint32 unk9;
    uint32 unk10;
    uint32 unk11; // always 4?

    if (unk7 != 0)
    {
        uint32 variableCount;
        uint32 additionalInts[variableCount];
    }

    FSeek(lastPos);
    FSkip(4);
  } unk_str1[header.fileCollectionCount] <optimize=false, read=UNKSTR1READ>;
} fileCollection;

FSeek(header.stringTableOffset);
struct TSCB_STRINGTABLE {
  uint32 unk0;
  uint32 unk1;
  uint32 unk2;
  uint32 unk3;
     
  local int num;
  while (!FEof()) {
    struct TSCB_NAMESTRING {
      // Skip any padding (these strings are null-padded to 4-byte boundaries)
      while (ReadByte() == 0) {
        FSeek(FTell() + 1);
      }
    
      string name;
    } namestring;
  }

} stringtable;
